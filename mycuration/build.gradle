apply plugin: 'jacoco'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'spoon'

android {
    compileSdkVersion 25
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.phicdy.mycuration"
        minSdkVersion 14
        targetSdkVersion 25
        versionName "1.5.2"
        versionCode 32

        testApplicationId "com.phicdy.mycuration.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias "$RELEASE_KEY_ALIAS"
            storeFile file("$RELEASE_STORE_FILE_PATH")
            keyPassword "$RELEASE_KEY_PASSWORD"
            storePassword "$RELEASE_STORE_PASSWORD"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable "RtlHardcoded"
        abortOnError true
    }

    flavorDimensions "mode"
    productFlavors {
        production {
            dimension "mode"
            minSdkVersion 14
        }
        uiTest {
            dimension "mode"
            minSdkVersion 18
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

jacoco {
    toolVersion = "0.7.7.201606060606"
}

// A list of directories which should be included in coverage report
def coverageSourceDirs = ['src/main/java', 'src/main/kotlin']
// A list of files which should be excluded from coverage report since they are generated and/or framework code
def coverageExcludeFiles = ['**/R.class', '**/R$*.class', '**/com/android/**/*.*',
                            '**/BuildConfig.class', '**/*Activity*.class',
                            '**/*Fragment*.class', '**/*Receiver.class',
                            '**/*Manifest*.class', '**/*Application*.class',
                            '**/com/phicdy/mycuration/data/db/*.*',
                            '**/com/phicdy/mycuration/domain/alarm/*.*',
                            '**/com/phicdy/mycuration/data/filter/Filter.class',
                            '**/com/phicdy/mycuration/data/filter/FilterFeedRegistration.class',
                            '**/com/phicdy/mycuration/data/rss/Article.class',
                            '**/com/phicdy/mycuration/data/rss/Curation.class',
                            '**/com/phicdy/mycuration/data/rss/CurationCondition.class',
                            '**/com/phicdy/mycuration/data/rss/CurationSelection.class',
                            '**/com/phicdy/mycuration/data/rss/Feed*',
                            '**/com/phicdy/mycuration/domain/task/GetFeedIconTask*',
                            '**/com/phicdy/mycuration/domain/task/GetHatenaBookmark*',
                            '**/com/phicdy/mycuration/tracker/*.*']
task jacocoTestReport(type: JacocoReport, dependsOn: ['testUiTestDebugUnitTest']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
        xml.destination new File("${buildDir}/reports/jacoco/jacocoTestReport.xml")
        html.destination new File("${buildDir}/reports/jacoco/html")
        classDirectories =
                fileTree(dir: "${buildDir}/intermediates/classes/uiTest/debug",
                        exclude: coverageExcludeFiles) +
                fileTree(dir: "$buildDir/tmp/kotlin-classes/uiTestDebug",
                        excludes: coverageExcludeFiles)
    }
    sourceDirectories = files(coverageSourceDirs)
    executionData = files "${buildDir}/jacoco/testUiTestDebugUnitTest.exec"

    doLast {
        println "jacoco xml report has been generated to file://${buildDir}/reports/jacoco/jacocoTestReport.xml"
        println "jacoco html report has been generated to file://${reports.html.destination}/index.html"
    }
}

spoon {
    debug = true

    // To grant permissions to Android M >= devices */
    grantAllPermissions = true
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.9'
    uiTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    uiTestImplementation 'com.android.support.test:runner:1.0.2'
    uiTestImplementation 'com.android.support.test:rules:1.0.2'
    uiTestImplementation 'com.squareup.spoon:spoon-client:1.6.4'
    implementation 'com.android.support:appcompat-v7:25.4.0'
    implementation 'com.android.support:support-v4:25.4.0'
    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    implementation 'org.jsoup:jsoup:1.10.2'
    implementation 'com.github.johnkil.android-progressfragment:progressfragment:1.4.0'
    implementation 'com.google.android.gms:play-services-analytics:11.8.0'
    implementation 'uk.co.chrisjenx:calligraphy:2.1.0'
    implementation 'com.android.support:design:25.4.0'
    implementation 'com.github.deano2390:MaterialShowcaseView:1.1.0@aar'
    implementation 'com.facebook.stetho:stetho:1.4.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.10'
    implementation 'com.android.support:recyclerview-v7:25.4.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
}

apply plugin: 'com.google.gms.google-services'

task copyBuild(type: Copy) {
    from 'build/outputs/apk/production/debug'
    into "$COPY_BUILD_DESTINATION"
    include '**/*.apk'
}

afterEvaluate { project ->
    project.tasks.assembleProductionDebug << {
        println "$COPY_BUILD_DESTINATION"
        tasks.copyBuild.execute()
    }
}

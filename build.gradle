// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
        maven { url "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
    }
    dependencies {
        classpath libs.gradle
        classpath 'com.google.gms:google-services:4.4.2'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.2'
        classpath libs.dagger.hilt.gradle
        classpath libs.sqldelight.gradle
    }
}

plugins {
    alias(libs.plugins.kotlin.gradle) apply(false)
    alias(libs.plugins.compose.compiler) apply(false)
    alias(libs.plugins.android.test) apply false
    alias(libs.plugins.baselineprofile) apply false
    alias(libs.plugins.android.application) apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://jitpack.io"
            content {
                includeGroup 'com.github.deano2390'
            }
        }
    }
}

subprojects {
    if (name == 'app') {
        apply plugin: 'com.android.application'
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-kapt'
        apply from: "$rootDir/jacoco.gradle"
    } else if (name == 'benchmark') {
        apply plugin: 'com.android.test'
        apply plugin: 'kotlin-android'
    } else if (name == 'baselineprofile') {
        // Do notihng
        return
    } else {
        apply plugin: 'com.android.library'
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-kapt'
        apply from: "$rootDir/jacoco.gradle"
    }

    android {
        compileSdkVersion 34

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 34
            versionName "1.12.0"
            versionCode 62
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }

        kotlinOptions {
            jvmTarget = '17'
        }

        testOptions {
            unitTests.returnDefaultValues = true
        }

        lintOptions {
            // Bug in Gradle plugin 4.0.0
            disable 'RequiredSize'
        }
    }

    if (name != 'benchmark') {
        dependencies {
            def composeBom = platform(libs.compose.bom)
            implementation composeBom
            implementation 'androidx.compose.material:material'
            implementation 'androidx.compose.foundation:foundation'
            implementation 'androidx.compose.ui:ui'
            implementation 'androidx.compose.ui:ui-tooling-preview'
            debugImplementation 'androidx.compose.ui:ui-tooling'
            implementation 'androidx.compose.material:material-icons-core'
            implementation 'androidx.compose.material:material-icons-extended'
            implementation 'androidx.compose.runtime:runtime-livedata'

            implementation 'androidx.activity:activity-compose:1.9.1'
            implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.8.4'

            testImplementation libs.junit
            testImplementation 'org.mockito.kotlin:mockito-kotlin:5.4.0'
            testImplementation libs.assertj
        }
    }
}


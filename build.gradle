// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.jetpack_compose_version = '1.0.2'
    ext.kotlin_version = '1.5.21'
    ext.coroutine_version = '1.5.0'
    ext.lifecycle_version = '2.3.1'
    ext.arch_core_testing_version = '2.1.0'
    ext.appcompat_version = '1.3.1'
    ext.fragment_ktx_version = '1.3.6'
    ext.preference_version = '1.1.1'
    ext.browser_version = '1.3.0'
    ext.recyclerview_version = '1.2.1'
    ext.constraintlayout_version = '2.1.0'
    ext.material_version = '1.4.0'
    ext.retrofit_version = '2.9.0'
    ext.rxjava_version = '2.2.21'
    ext.rxandroid_version = '2.2.21'
    ext.firebase_bom_version = '28.4.0'
    ext.jacoco_version = "0.8.7"
    ext.glideVer = '4.12.0'
    ext.work_version = '2.6.0'
    ext.dagger_hilt_version = '2.38.1'
    ext.sql_delight_version = '1.5.1'
    ext.sqlite_version = '2.1.0'
    ext.timber_version = '4.7.1'

    repositories {
        google()
        mavenCentral()
        maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
        maven { url "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.2'
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.1'
        classpath "com.google.dagger:hilt-android-gradle-plugin:$dagger_hilt_version"
        classpath 'com.squareup.sqldelight:gradle-plugin:1.5.0'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven {
            url "https://jitpack.io"
            content {
                includeGroup 'com.github.deano2390'
            }
        }
    }
}

subprojects {
    if (name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply from: "$rootDir/jacoco.gradle"

    android {
        compileSdkVersion 30

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 30
            versionName "1.8.5"
            versionCode 58

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildFeatures {
            compose true
        }

        composeOptions {
            kotlinCompilerExtensionVersion jetpack_compose_version
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

        kotlinOptions {
            jvmTarget = '11'
        }

        testOptions {
            unitTests.returnDefaultValues = true
        }

        lintOptions {
            // Bug in Gradle plugin 4.0.0
            disable 'RequiredSize'
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        implementation "androidx.compose.ui:ui:$jetpack_compose_version"
        implementation "androidx.compose.ui:ui-tooling:$jetpack_compose_version"
        implementation "androidx.compose.foundation:foundation:$jetpack_compose_version"
        implementation "androidx.compose.material:material:$jetpack_compose_version"
        implementation "androidx.compose.material:material-icons-core:$jetpack_compose_version"
        implementation "androidx.compose.material:material-icons-extended:$jetpack_compose_version"
        implementation "androidx.compose.runtime:runtime-livedata:$jetpack_compose_version"
        implementation 'androidx.activity:activity-compose:1.3.1'
        implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'

        testImplementation 'junit:junit:4.13.1'
        testImplementation 'org.mockito.kotlin:mockito-kotlin:3.2.0'
        testImplementation 'org.assertj:assertj-core:3.11.1'
    }
}


apply plugin: 'jacoco'

jacoco {
    toolVersion = jacoco_version
}

task jacocoMerge(type: JacocoMerge) {
    gradle.afterProject { p, state ->
        if (p.rootProject != p && p.plugins.hasPlugin('jacoco')) {
            executionData file("${p.buildDir}/jacoco").listFiles().findAll {
                it.name.endsWith(".exec")
            }
        }
    }
}

def coverageExcludeFiles = ['**/R.class', '**/R$*.class', '**/com/android/**/*.*',
                            '**/BuildConfig.class', '**/*Activity*.class',
                            '**/*Fragment*.class', '**/*Receiver.class',
                            '**/*Manifest*.class', '**/*Application*.class',
                            '**/com/phicdy/mycuration/data/db/*.*',
                            '**/com/phicdy/mycuration/data/repository/*.*',
                            '**/com/phicdy/mycuration/data/network/*',
                            '**/com/phicdy/mycuration/di/*.*',
                            '**/com/phicdy/mycuration/domain/alarm/*.*',
                            '**/com/phicdy/mycuration/domain/entity/Filter*',
                            '**/com/phicdy/mycuration/domain/entity/Article.class',
                            '**/com/phicdy/mycuration/domain/entity/Curation.class',
                            '**/com/phicdy/mycuration/domain/entity/CurationCondition.class',
                            '**/com/phicdy/mycuration/domain/entity/CurationSelection.class',
                            '**/com/phicdy/mycuration/domain/entity/Feed*',
                            '**/com/phicdy/mycuration/domain/task/GetFeedIconTask*',
                            '**/com/phicdy/mycuration/presentation/view/*',
                            '**/com/phicdy/mycuration/util/log/*',
                            '**/com/phicdy/mycuration/tracker/*.*']
task jacocoMergedReport(type: JacocoReport, dependsOn: [tasks.jacocoMerge]) {
    executionData jacocoMerge.destinationFile
    def sources = []
    subprojects.forEach {
        sources += "${it.projectDir.path}/src/main/java"
    }
    getSourceDirectories().setFrom(files(sources))
    getClassDirectories().setFrom(
            fileTree(
                dir: ".",
                includes: ["**/build/intermediates/classes/debug/**",
                           "**/build/tmp/kotlin-classes/debug/**"],
                excludes: coverageExcludeFiles
            )
    )

    reports {
        xml.enabled = true
        html.enabled true
        csv.enabled false
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

task testDebugUnitTest(dependsOn: [tasks.jacocoMergedReport])
